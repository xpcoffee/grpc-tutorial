plugins {
    id 'java'
    id 'com.google.protobuf' version '0.8.14'
    id 'idea'
    id 'application'
}
group 'com.xpcoffee'
version '1.0'

repositories {
    mavenCentral()
}

application {
    mainClass = 'server.MovieFinderServer'
}

jar {
    manifest.attributes['Main-Class'] = 'server.MovieFinderServer'

    // don't pull in duplicate classes from dependencies
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    // this makes sure dependencies are in the JAR
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.12.0'
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.34.1'
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

dependencies {
    implementation 'io.grpc:grpc-netty-shaded:1.34.1'
    implementation 'io.grpc:grpc-protobuf:1.34.1'
    implementation 'io.grpc:grpc-stub:1.34.1'

    implementation project(':movie-api')
    implementation project(':movie-store')
    implementation project(':recommender')
    implementation project(':user-preferences')

    compileOnly 'org.apache.tomcat:annotations-api:6.0.53'
}
